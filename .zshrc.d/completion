#!/hint/zsh

#
# completion settings
#

local -a _compinit


#
# cache setup
#

local _zbasedir="$(systemd-path user-state-cache)/zsh"

local _zcompdump=$_zbasedir/zcompdump
if mkdir -p ${_zcompdump%/*}; then
	_compinit+=( -d $_zcompdump )
fi

local _zcompcache=$_zbasedir/zcompcache
if mkdir -p $_zcompcache; then
	zstyle ':completion:*' use-cache true
	zstyle ':completion:*' cache-path $_zcompcache
fi


#
# common zstyles
#

zstyle ':completion:*' auto-description 'argument: %d'
zstyle ':completion:*' completer _complete _approximate
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' format '[%d]'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd ..
zstyle ':completion:*' insert-unambiguous false
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt $l (%p): Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[-_./]=* r:|=*'
zstyle ':completion:*' max-errors 10 numeric
zstyle ':completion:*' menu select
zstyle ':completion:*' original true
zstyle ':completion:*' prompt '%e corrections made:'
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %l (%p)%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*' special-dirs false
zstyle :compinstall filename '/home/intelfx/.zshrc'

zstyle ':completion:*:hosts' known-hosts-files \
	/etc/ssh/ssh_known_hosts \
	"$HOME"/.ssh/known_hosts \
	"$HOME"/.ssh/known_hosts.*^(.old)(N)


#
# specific zstyles
#

zstyle ':completion:*:sysctl:*' delimiter '.'
zstyle ':completion:*:sysctl:*' expand  # reset to empty because _multi_part is broken

# XXX: for some reason, `hosts-host` tag must have an explicit name, otherwise the `hosts-ipaddr` tag breaks
# (#zsh did not come up with a better answer)
zstyle ':completion:*:(ssh|scp|rsync):*' tag-order 'hosts:-host:remote\ host\ name hosts:-domain:domain hosts:-ipaddr:ip\ address *'
zstyle ':completion:*:(scp|rsync):*' group-order users files all-files hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*'         group-order                       hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-host' ignored-patterns '*.*' loopback localhost
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-domain' ignored-patterns '<->.<->.<->.<->' '^*.*' '*@*'
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-ipaddr' ignored-patterns '^<->.<->.<->.<->' '127.0.0.<->'


#
# {,bash}compinit
#

_compinit+=(
	-u # do not whine about ~/.zshrc/fpath in `sudo -s`
)
autoload -Uz compinit
compinit $_compinit

autoload -U +X bashcompinit
bashcompinit
